clear
Set DefaultBaseFrequency=50  ! for European system
new circuit.send_network

! Source voltage changed to that the daytime voltage at sub11 matches MPN
Edit Vsource.Source basekV=132 pu=1.0332 R1=0.0001 X1=0.0001 R0=0.0001 X0=0.0001

new transformer.sourceXfmr phases=3 buses=[sourcebus sub11] kVs=[132 11] kvas=[39000 39000]
~ conns=[Wye Wye] xhl=13.90597023  %Rs=[0.167434576, 0.167434576]
~ mintap=0.9 maxtap=1.20 numtaps=20

! Add the linecodes and 4.1 km line from WPD xfmr
redirect linecodes.dss
new line.wpd11kv bus1=sub11 bus2=SS13 phases=3 linecode=al_c3_xlpe_swa_300 len=4.1 units=km

! Add majority of network components
Redirect xfmrs_full.dss
Redirect lines_full.dss
Redirect loads_full.dss
Redirect generators.dss

! some unusual LV voltage bases for solar & wind generators
set voltagebases="132.0,11.0,0.4,0.6,0.69"
calcvoltagebases

! update voltages that need manual updating
setkvbase RMU1_LV 0.6
setkvbase RMU2_LV 0.6

! add buscoords and energymeter to enable plotting
buscoords buscoords.csv
new energymeter.m1 element=line.wpd11kv

! Loadmult - at 1.0, transformers are at their nominal power rating
set loadmult=1.0

! Finally solve
solve

!set marktransformers=true
!set markcapacitors=true
!set markregulators=true
plot
!plot profile phases=all